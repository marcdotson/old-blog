---
title: "Non-centered parameterization for hierarchical models"
author: "Marc Dotson"
date: '2020-03-15'
slug: non-centered
---



<p>In a <a href="https://www.occasionaldivergences.com/post/stan-hierarchical/">previous post</a>, we provided a gentle introduction to building hierarchical Bayesian models in Stan. We quickly ran into divergences (i.e., divergent transitions) when attempting to estimate our model. While hierarchical models have posteriors with <a href="https://arxiv.org/abs/1312.0906">geometry that is difficult to navigate</a>, we were able to initially fix the problem by more carefully navigating the posterior with a smaller step size.</p>
<p>However, the posterior geometry of hierarchical models typically requires a non-centered parameterization, expressing the same model in a different way to ease the navigation of the posterior. The goal of this post is to expand on that <a href="https://www.occasionaldivergences.com/post/stan-hierarchical/">previous post</a> by providing a walkthrough of implementing a non-centered parameterization for a standard hierarchical linear Bayesian model in Stan.</p>
<div id="non-centered-parameterization" class="section level2">
<h2>Non-centered parameterization</h2>
<p>The standard centered parameterization for a simple hierarchical regression has a population model and likelihood:</p>
<pre><code>beta ~ normal(mu, tau)
y ~ normal(beta, sigma)</code></pre>
<p>where we get draws from the posterior distribution of <code>mu</code> and <code>tau</code>, the population mean and variance, and <code>beta</code>, the group-level intercepts.</p>
<p>A non-centered parameterization re-expresses the population model and likelihood for a simple hierarchical regression as:</p>
<pre><code>delta ~ normal(0, 1)
beta = mu + tau * delta
y ~ normal(beta, sigma)</code></pre>
<p>where we get draws from the posterior distribution of <code>mu</code>, <code>tau</code>, and <code>delta</code>, since <code>beta</code> is now a fixed transformation of the other parameters (note the <code>=</code> instead of <code>~</code> for <code>beta</code>). The benefit of a non-centered parameterization (i.e., the inclusion of the intermediate <code>delta</code> and the fixed transformation of <code>beta</code>) is that the dependencies between the two layers in the hierarchy are broken, producing a simpler posterior geometry.</p>
</div>
<div id="simple-hierarchical-regression" class="section level2">
<h2>Simple hierarchical regression</h2>
<p>Letâ€™s start by demonstrating how to implement a non-centered parameterization for a simple hierarchical regression.</p>
<pre class="stan"><code>// Index values and observations.
data {
  int&lt;lower = 1&gt; N;               // Number of observations.
  int&lt;lower = 1&gt; K;               // Number of groups.
  vector[N] y;                    // Vector of observations.
  int&lt;lower = 1, upper = K&gt; g[N]; // Vector of group assignments.
}

// Parameters and hyperparameters.
parameters {
  real mu;                        // Mean of the population model.
  real&lt;lower = 0&gt; tau;            // Variance of the population model.
  vector[K] delta;                // Vector of non-centered group intercepts.
  real&lt;lower = 0&gt; sigma;          // Variance of the likelihood.
}

// Deterministic transformation.
transformed parameters {
  // Vector of transformed group intercepts.
  vector[K] beta;

  // Non-centered parameterization.
  for (k in 1:K) {
    beta[k] = mu + tau * delta[k];
  }
}

// Hierarchical regression.
model {
  // Hyperpriors.
  mu ~ normal(0, 5);
  tau ~ normal(0, 5);

  // Prior.
  sigma ~ normal(0, 5);

  // Non-centered population model and likelihood.
  for (k in 1:K) {
    delta[k] ~ normal(0, 1);
  }
  for (n in 1:N) {
    y[n] ~ normal(beta[g[n]], sigma);
  }
}</code></pre>
<p>Instead of <code>beta</code>, we include <code>delta</code> in the <code>parameters</code> block, noted here as the non-centered group intercepts. To impose the deterministic transformation, we include a <code>transformed parameters</code> block, which includes both <code>beta</code> and the non-centered transformation itself <code>beta[k] = mu + tau * delta[k]</code>. Finally, in place of <code>beta</code>, the non-centered population model <code>delta[k] ~ normal(0, 1)</code> is included in the <code>model</code> block.</p>
</div>
<div id="multiple-hierarchical-regression" class="section level2">
<h2>Multiple hierarchical regression</h2>
<ul>
<li>Add lower-level covariates <code>X</code>.</li>
<li>Add upper-level covariates <code>Z</code>.</li>
</ul>
<hr />
<div id="marc-dotson" class="section level3">
<h3>Marc Dotson</h3>
<p>Marc is an assistant professor of marketing at the BYU Marriott School of Business. He graduated with an MSc from The London School of Economics and Political Science in 2009 and a PhD from The Ohio State University in 2016. His research interests include Bayesian inference, predictive modeling, consumer preference heterogeneity, and unstructured data. Marc teaches marketing analytics. You can find him on <a href="https://twitter.com/marcdotson">Twitter</a> and <a href="https://github.com/marcdotson">GitHub</a>.</p>
</div>
</div>
